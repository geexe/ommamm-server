//New World Challenge

izlude,108,131,3	script	The Sign Challenge::endgame_thesign	804,{
	mes "[Challenge]";
	mes "Hello, " + strcharinfo(0) + "!";
	mes "Welcome to Ommamm Challenge.";
	mes " ";
	mes "Prove yourself that you really love Ragnarok Online.";
	mes "We will reward only the utmost dedication to the game.";
	
	next;
	mes "[Challenge]";
	mes "This is a challenge to prove that your perseverence is limitless.";
	next;
	switch(select("Quest Info:Claim Reward:Cancel")){
	case 1:
		mes "[Challenge]";
		mes "You must complete the quest:";
		mes "^e53535The Sign^000000";
		
		next;
		mes "[Challenge]";
		mes "When you have completed the quest, you can claim the reward from me.";
		mes " ";
		mes "The reward for this quest is";
		for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
			mes (.@k+1) + ". " + "^4ba94b" + getitemname(.reward[.@k]) + "^000000" + " x " + "^1c3f89" + .reward_quantity[.@k] + "^000000";
		}
		break;
	case 2:
		mes "[Challenge]";
		//check to see if character has received the reward
		if(!getommammreward_thesign) {
			//check to see if all MVPs have been completed
			if(countitem(2644) || getequipid(7) == 2644 || getequipid(8) == 2644) {
				mes "You have completed the challenge";
				mes "Do you want to claim the reward?";
				next;
				switch(select("Yes:No")) {
					case 1: 
						break;
					case 2:
						mes "[Challenge]";
						mes "Come back when you want to claim the reward";
						close;
					}
				
				//check weight and inventory 
				if(checkweight(.reward,1)) {
					//Set variable to prevent player from getting multiple items
					getommammreward_thesign = 1;
					for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
						getitembound .reward[.@k], .reward_quantity[.@k], 1;
					}
					mes "[Challenge]";
					mes "Congratulations on your achievement! Enjoy your reward.";
				}
				else {
					mes "[Challenge]";
					mes "You do not enough space.";
					mes "Please clear your inventory and come back again.";
				}
			}
			else {
				mes "You have not completed the challenge.";
			}
		}
		else {
			mes "You have already received reward for this challenge.";
		}
		break;	
	case 3:
		break;
	}
	close;
	
	
OnInit:
	//reward item. Add more reward by <reward 1>, <reward 2>, <reward 3>, ....
	//reward_quantity set in the same manner <quantity for reward 1>, <quantity for reward 2>, ...
	setarray .reward[0],969,18503,7227,16755,16753;	
	setarray .reward_quantity[0],6,1,350,3,3;
	end;
	}
