//Biolab Hunting Challenge

izlude,148,129,3	script	Challenge::endgame_npc	811,{
	mes "[Challenge]";
	mes "Hello, " + strcharinfo(0) + "!";
	mes "Welcome to Ommamm Challenge.";
	mes "Please select a challenge to view the details";
	//switch for other challenges
	next;
	mes "[Challenge]";
	mes "This is a challenge to prove that your skills are worthy of recognition";
	next;
	switch(select("Quest Info:Claim Reward:Cancel")){
	case 1:
		mes "[Challenge]";
		mes "You must kill all 6 Biolab MVPs:";
		for (.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
			mes .@i+1 + ". " + strmobinfo(1,.biolabmvp[.@i]);
		}
		next;
		mes "[Challenge]";
		mes "When you have killed all 6 MVPs, you can claim the reward from me.";
		mes " ";
		mes "The reward for this quest is";
		mes getitemname(.reward);
		next;
		//display completed MVPs
		if(getarraysize(#biolabkill)) {
			mes "MVPs you have completed:";
			for (.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
				if(#biolabkill[.@i] == .biolabmvp[.@i]) {
					mes strmobinfo(1,#biolabkill[.@i]);
				}
			}
		}
		else {
			mes "Go kill those MVPs!";
		}
		break;
	case 2:
		mes "[Challenge]";
		//check to see if character has received the reward
		if(!#getommammreward) {
			for (.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
				if(#biolabkill[.@i] == .biolabmvp[.@i]) {
					.@mvpcount += 1;
				}
			}
			//check to see if all MVPs have been completed
			if(.@mvpcount == getarraysize(.biolabmvp)) {
				mes "You have completed the challenge";
				mes "Do you want to claim the reward?";
				
				//check weight and inventory 
				if(checkweight(.reward,1)) {
					//Set variable to prevent player from getting multiple items
					#getommammreward = 1;
					getitem .reward, 1;
				}
				else {
					mes "You do not enough space.";
					mes "Please clear your inventory and come back again.";
				}
			}
			else {
				mes "You have not completed the challenge.";
			}
		}
		else {
			mes "You have already received reward for this challenge.";
		}
		break;	
	case 3:
		break;
	}
	close;
	
OnNPCKillEvent:
	for (.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
		if(killedrid == .biolabmvp[.@i]) {
			setarray #biolabkill[.@i], killedrid;
			dispbottom "[Challenge] Biolab MVP killed: " + strmobinfo(1,killedrid);
		}
	}
	end;
	
OnInit:
	//monsters to kill
	setarray .biolabmvp[0],1646,1647,1648,1649,1650,1651;
	//reward item
	.reward = 501;			
	end;
	}
