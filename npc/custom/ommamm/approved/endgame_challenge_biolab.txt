//Biolab Hunting Challenge

izlude,100,140,5	script	Biolab Challenge::endgame_biolab	904,{
	mes "[Challenge]";
	mes "Hello, " + strcharinfo(0) + "!";
	mes "Welcome to Ommamm Challenge.";
	mes " ";
	mes "Prove yourself that you really love Ragnarok Online.";
	mes "We will reward only the utmost dedication to the game.";
	
	next;
	switch(select("Quest Info:Claim Reward:Cancel")){
	case 1:
		mes "[Challenge]";
		mes "You must kill all " + getarraysize(.biolabmvp) + " Biolab MVPs:";
		for(.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
			mes .@i+1 + ". " + "^e53535" + strmobinfo(1,.biolabmvp[.@i]) + "^000000";
			if(checkquest(70000 + .biolabmvp[.@i]) < 0) {
				setquest(70000 + .biolabmvp[.@i]);
			}
		}
		next;
		mes "[Challenge]";
		mes "When you have killed all " + getarraysize(.biolabmvp) + " MVPs, you can claim the reward from me.";
		next;
		mes "[Challenge]";
		mes "The reward for this quest is";
		for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
			mes (.@k+1) + ". " + "^4ba94b" + getitemname(.reward[.@k]) + "^000000" + " x " + "^1c3f89" + .reward_quantity[.@k] + "^000000";
		}
		next;
		//display completed MVPs
		for(.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
			if(checkquest(70000 + .biolabmvp[.@i],HUNTING) == 2) {			
				.@mvpcount++;
			}
		}
		if(!.@mvpcount) {
			mes "[Challenge]";
			mes "You have not defeated any Biolab boss.";
			mes "Go kill those MVPs!";
			close;
		}
		mes "[Challenge]";
		mes "MVPs you have completed:";
		for(.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
			if(checkquest(70000 + .biolabmvp[.@i],HUNTING) == 2) {			
				mes strmobinfo(1,.biolabmvp[.@i]);
			}
		}
		close;
		break;
	case 2:
		//check to see if character has received the reward
		if(getommammreward_biolab) {
			mes "[Challenge]";
			mes "You have already received reward for this challenge.";
			close;
		}
		//check to see if all MVPs have been completed
		for(.@i = 0; .@i < getarraysize(.biolabmvp); .@i++) {
			if(checkquest(70000 + .biolabmvp[.@i],HUNTING) < 2) {
				mes "[Challenge]";
				mes "You have not completed the challenge.";
				close;
			}
		}	
		mes "You have completed the challenge";
		mes "Do you want to claim the reward?";
		switch(select("Yes:No")) {
			case 1: 
				break;
			case 2:
				mes "[Challenge]";
				mes "Come back when you want to claim the reward";
				close;
		}
		//check weight and inventory 
		if(!checkweight2(.reward,.reward_quantity)) {
			mes "You do not have enough space.";
			mes "Please clear your inventory and come back again.";
			close;
		}
		//Set variable to prevent player from getting multiple items
		getommammreward_biolab = 1;
		//Cycle through the reward array
		for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
			getitembound .reward[.@k], .reward_quantity[.@k], 1;
		}
		mes "[Challenge]";
		mes "Congratulations on your achievement! Enjoy your reward.";
		close;
		break;	
	case 3:
		break;
	}
	close;
	
OnInit:
	//monsters to kill
	setarray .biolabmvp[0],
		1646, //Lord Knight Seyren
		1647, //Assassin Cross Eremes
		1648, //Whitesmith Howard
		1649, //High Priest Magaretha
		1650, //Sniper Cecil
		1651, //High Wizard Kathryne
		2235, //Paladin Randel
		2236, //Creator Flmel
		2237, //Professor Celia
		2238, //Champion Chen
		2239, //Stalker Gertie
		2240, //Clown Alphoccio
		2241; //Gypsy Trentini
		
	//reward item. Add more reward by <reward 1>, <reward 2>, <reward 3>, ....
	//reward_quantity set in the same manner <quantity for reward 1>, <quantity for reward 2>, ...
	setarray .reward[0],31100,6238,6232,2383,16753,16755;	
	setarray .reward_quantity[0],1,1,1,1,15,15;
	end;
	}
