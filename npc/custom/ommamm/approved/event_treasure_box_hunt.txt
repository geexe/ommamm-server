-	script	Treasure_Box_Hunt	-1,{

Event_start:
	.event_end = 0;
	$@killcount = 0;
	$@spawn_map$ = .all_towns$[rand(getarraysize(.all_towns$))];
	killmonster $@spawn_map$, "Treasure_Box_Hunt::OnTreasureDie";
	$@spawn_count = rand(10,20);
	announce "Treasure Event: กล่องสมบัติ Treasure Box ปรากฏตัว ณ เมือง "+$@spawn_map$+" จำนวน [ "+$@spawn_count+" ] กล่อง มีเวลาในการล่าสมบัติ 5 นาที ",0,0xFFFF00;
	monster $@spawn_map$,0,0,"Treasure Box",1474,$@spawn_count,"Treasure_Box_Hunt::OnTreasureDie";
	sleep 300000;
	if(!.event_end) {
		goto Event_end;
	}
	end;
	
OnTreasureDie:
	$@killcount += 1;
	for (.@i = 0; .@i < getarraysize(.reward); .@i++) {
		getitem .reward[.@i], .reward_quantity[.@i];
	}
	announce "ผู้เล่น [ " + strcharinfo(0) + " ] ได้ค้นพบ Treasure Box กล่องที่ "+$@killcount+" / "+$@spawn_count+" ",0,0xFF5555;
	if($@killcount == $@spawn_count) {
		goto Event_end;
	}
	end;
	
Event_end:
	.event_end = 1;
	killmonster $@spawn_map$, "Treasure_Box_Hunt::OnTreasureDie";
	announce "Treasure Event : หมดเวลาในการล่าสมบัติ Treasure Box ทุกกล่องถูกทำลาย",0,0xFFFF00;
	end;

OnMinute00:
	goto Event_start;
	end;	
	
OnInit:
	//Reward when monsters are killed
	setarray .reward[0],7227;
	setarray .reward_quantity[0],1;
	//Town array for random
	setarray .all_towns$[0],
		"prontera",
		"alberta",
		"aldebaran",
		"amatsu",
		"ayothaya",
		"brasilis",
		"comodo",
		"dewata",
		"eclage",
		"einbech",
		"einbroch",
		"dicastes01",
		"geffen",
		"gonryun",
		"hugel",
		"izlude",
		"jawaii",
		"lighthalzen",
		"louyang",
		"xmas",
		"malangdo",
		"malaya",
		"manuk",
		"mid_camp",
		"mora",
		"morocc",
		"moscovia",
		"nameless_n",
		"niflheim",
		"payon",
		"rachel",
		"splendide",
		"thor_camp",
		"umbala",
		"veins",
		"yuno";
	end;
}