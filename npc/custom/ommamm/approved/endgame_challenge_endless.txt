//Endless Tower Challenge

izlude,105,140,3	script	Endeless Tower Challenge::endgame_endless	709,{
	mes "[Challenge]";
	mes "Hello, " + strcharinfo(0) + "!";
	mes "Welcome to Ommamm Challenge.";
	mes " ";
	mes "Prove yourself that you really love Ragnarok Online.";
	mes "We will reward only the utmost dedication to the game.";
	next;
	mes "[Challenge]";
	mes "This is a challenge to prove that your knowledge and preparation are second to none.";
	next;
	switch(select("Quest Info:Claim Reward:Cancel")){
	case 1:
		mes "[Challenge]";
		mes "You must ascend 100 floors of the Endless Tower, slay the bosses at the top of the tower and liberate";
		mes "^e53535Lost Souls^000000";
		next;
		mes "[Challenge]";
		mes "When you have completed Endless Tower and liberated Lost Souls, you can claim the reward from me.";
		next;
		mes "[Challenge]";
		mes "The reward for this quest is";
		for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
			mes (.@k+1) + ". " + "^4ba94b" + getitemname(.reward[.@k]) + "^000000" + " x " + "^1c3f89" + .reward_quantity[.@k] + "^000000";
		}
		next;
		mes "[Challenge]";
		mes "Lost Souls await salvation.";
		break;
	case 2:
		mes "[Challenge]";
		//check to see if character has received the reward
		if(!getommammreward_endless) {
		
			//check to see if ET has been completed
			if(endless_complete) {
				mes "You have completed the challenge";
				mes "Do you want to claim the reward?";
				
				//check weight and inventory 
				if(checkweight2(.reward,.reward_quantity)) {
					//Set variable to prevent player from getting multiple items
					getommammreward_endless = 1;
					for(.@k = 0; .@k < getarraysize(.reward); .@k++) {
						getitembound .reward[.@k], .reward_quantity[.@k], 1;
					}
				}
				else {
					mes "You do not have enough space.";
					mes "Please clear your inventory and come back again.";
				}
			}
			else {
				mes "You have not completed the challenge.";
			}
		}
		else {
			mes "You have already received reward for this challenge.";
		}
		break;	
	case 3:
		break;
	}
	close;
	
OnInit:
	//reward item. Add more reward by <reward 1>, <reward 2>, <reward 3>, ....
	//reward_quantity set in the same manner <quantity for reward 1>, <quantity for reward 2>, ...
	setarray .reward[0],20737,6233,6228,16755,16753;	
	setarray .reward_quantity[0],1,1,1,5,5;
	end;
	}
