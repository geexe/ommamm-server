izlude,127,139,3	script	Emmitt White::gef_magic_init	641,{
	
	.npc$ = "[Geffen Magic Tournament]";
	
	if(checkquest(70001,PLAYTIME) == 0 || checkquest(70001,PLAYTIME) == 1 ) {
		mes .npc$;
		mes "You can participate in the tournament again after 23 hours.";
		close;
	} else if (checkquest(70001,PLAYTIME) == 2) {
		erasequest(70001);
	}
	mes .npc$;
	mes "Welcome to Geffen Magic Tournament.";
	next;
	mes .npc$;
	mes "This was a tournament held in the ancient past, where magicians around Rune-Midgard convened to find the true master of magic.";
	next;
	mes .npc$;
	mes "Here you will be battling various heroes in a tournament manner:";
	mes "1-On-1 for 12 rounds";
	next;
	mes .npc$;
	mes "Are you interested in joining the tournament?";
	next;
	switch(select("Sure:I would rather not risk my life...")) {
	case 1:
		break;
	case 2:
		mes .npc$;
		mes "I would not blame you for that!";
		close;
	}
	mes .npc$;
	mes "That's the spirit!";
	mes "Are you ready to enter the time gap?";
	next;
	switch(select("Enter:I need more time to prepare")) {
	case 1:
		break;
	case 2:
		mes .npc$;
		mes "Come back when you are ready.";
		close;
	}
	getpartymember getcharid(1);
	if($@partymembercount > 1) {
		mes .npc$;
		mes "Only 1 person is allowed in the party.";
		close;
	}
	if(instance_create("Geffen Magic Tournament") < 0) {
		mes "Can't generate instance.";
		close;
	};
	next;
	switch(instance_enter("Geffen Magic Tournament")) {
	case 3:
		mes "An unknown error has occurred.";
		close;
	case 2:
		mes "The memorial dungeon Geffen Magic Tournament does not exist.";
		mes "The party leader did not generate the dungeon yet.";
		close;
		break;
	case 1:
		mes "You can enter the dungeon after making the party.";
		close;
		break;
	case 0:
		setquest 70001;
		break;
	}	
}

1@gef,122,58,4	script	Receptionist::gef_magic_reception	937,{
	
	.npc$ = "[Tournament Receptionist]";
	
	if('warp_stadium) {
		mes .npc$;
		mes "The Magic Tournament is going to start soon.";
		mes "Enter the portal to the south and you can join in.";
		next;
		mes .npc$;
		mes "Good luck!";
		close;
	}
	if('test_success) {
		mes .npc$;
		mes "Congratulations! You passed the test!";
		mes "Give me your name so I can write it down.";
		mes "You will automatically be registered for the tournament now.";
		next;
		input .@name$;
		mes .npc$;
		mes strcharinfo(0) + "?";
		mes "Okay, interesting name.";
		mes "Anyway, your registration is complete!";
		next;
		mes .npc$;
		mes "Competitors who registered will randomly be selected.";
		next;
		mes .npc$;
		mes "The Magic Tournament is going to start soon.";
		mes "Enter the portal to the south and you can join in.";
		next;
		mes .npc$;
		mes "Good luck!";
		'warp_stadium = 1;
		enablenpc instance_npcname("gef_stadium_warp");
		close;
	}
	mes .npc$;
	mes "Thank you for visiting Geffen";
	mes "Magic Tournament.";
	mes "What can I do for you?";
	next;
	switch(select("About Geffen Magic Tournament:How do I Register?:Collecting Rewards:Can I get a Spectator Pass?:Cancel")) {
	case 1:
		mes .npc$;
		break;
	case 2:
		mes .npc$;
		mes "There is no registration fee to enter.";
		mes "The only requirement is to pass a simple test.";
		next;
		mes .npc$;
		mes "The test is very simple. Defeat a Desert Wolf in 60 seconds or less and you're in.";
		next;
		mes .npc$;
		mes "If you fail you will automatically be eliminated from the list but you can try again as many times as you wish.";
		mes "Do you want to take the test?";
		next;
		switch(select("No:Yes")) {
		case 1:
			mes .npc$;
			mes "Come back if you change your mind.";
			close;
		case 2:
			mes .npc$;
			mes "You can participate in the Magic Tournament after passing the test. Then, let us begin!";
			hideonnpc instance_npcname("gef_magic_reception");
			npctalk "The time limit is 60 seconds! Ready! Set! Go!";
			monster instance_mapname("1@gef"),118,62,"Desert Wolf",1106,1,instance_npcname("gef_magic_reception") + "::OnDesertWolfDead";
			setquest 70002;
			close2;
			break;
		}
		break;
	case 3:
	case 4:
		mes "<Placeholder>";
		close;
	case 5:
		close;
	}
	end;
	
OnDesertWolfDead:
	if(checkquest(70002,PLAYTIME) == 2) {
		hideoffnpc instance_npcname("gef_magic_reception");
		npctalk "You have failed! You can come back and try again.";
		erasequest 70002;
		end;
	}
	erasequest 70002;
	'test_success = 1;
	hideoffnpc instance_npcname("gef_magic_reception");
	npctalk "You passed the test! Shall we finish your registration?";
	end;
}

1@gef,120,35,0	script	gef_stadium_warp	45,2,2,{

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnTouch:	
	warp instance_mapname("1@ge_st"),104,50;
	end;
}

1@ge_st,109,57,4	script	Geffen Academy Master::gef_magic_mc	10006,{
	
	.npc$ = "[Magic Academy Master]";	
	
	if (!'round) {
		'round = 1;
		mes .npc$;
		mes "Ladies and gentlemen!";
		mes "Welcome to Geffen Magic Tournament!";
		'accountid = getattachedrid();
		next;
		mes .npc$;
		mes "Today a champion will be crowned.";
		next;
		mes .npc$;
		mes "There are 2 divisions this year divided into group A and B.";
		mes "The winner of each group will face each other for the championship match.";
		next;
		mes .npc$;
		mes "I am the MC of group A.";
		mes "Geffen Academy Master~!";
		mes "Archmeiji Janjiar Helmode";
		next;
		mes .npc$;
		mes "I'm honored to be this year's MC.";
		mes "Now it's time for the matches to begin.";
		next;
		mes .npc$;
		mes "Fight...";
		close2;
		sleep2 1000;
		npcskilleffect 51,0,90,48;
		npctalk "Ladies and Gentlemen!";
		sleep2 1000;
		npctalk "I do my best to be as loud as I can!";
		sleep2 1000;
		npctalk "The Geffen Magic Tournament will now commence!";
		enablenpc instance_npcname("gef_magic_round1");
		donpcevent instance_npcname("gef_magic_round1") + "::OnRound1Start";
		sleep2 1000;
		npctalk "We are starting the 1st match for Group A.";
		sleep2 1000;
		if(sex) {
			npctalk "On this side, he looks weak enough to lose in the 1st match!";
		} else {
			npctalk "On this side, she looks weak enough to lose in the 1st match!";
		}
		sleep2 1000;
		npctalk "Give a big welcome to the unknown competitor! " + strcharinfo(0) + "";
		sleep2 1000;
		npctalk "And on the other corner, Arhi the Novice! Give it your best shot!";
		end;
	}
	end;
	
OnRound1Won:
	'round = 2;
	npctalk "The first match of Group A is finished~! Each match winner will receive a reward";
	sleep2 2000;
	npctalk "You may take some time to heal your wounds.";
	sleep2 2000;
	npctalk "Please tell the Tournament Guide when you are ready for the next round.";
	enablenpc instance_npcname("gef_magic_guide");
	end;
	
OnRound2Start:
	attachrid('accountid);
	sleep2 1000;
	npctalk "The 2nd match of Group A has begun!";
	sleep2 1000;
	npctalk "To the weak looking competitor...";
	sleep2 1000;
	npctalk "Leaves will eventually fall " + strcharinfo(0) + "!";
	sleep2 2000;
	npctalk "Our next competitor is~!!!";
	sleep2 2000;
	npctalk "Despite his skipping classes at the Magic Academy...";
	sleep2 2000;
	npctalk "Big round of applause to Mage hopeful, Dio Anemos!";
	sleep2 2000;
	npctalk "Time limit is 3 minutes. If there is no winner after that...";
	sleep2 2000;
	npctalk "Let's get it on!";
	sleep2 2000;
	npctalk "A match has started. This is so exciting.";
	end;
	
OnRound2Won:
	'round = 3;
	npctalk "The 2nd match of Group A is finished~! Each match winner will receive a reward";
	sleep2 2000;
	npctalk "You will be given time to heal any injuries.";
	sleep2 2000;
	npctalk "Please tell the Tournament Guide when you are ready for the next round.";
	enablenpc instance_npcname("gef_magic_guide");
	end;

OnRoundEnd:
	end;
}

1@ge_st,107,57,4	script	Tournament Guide::gef_magic_guide	899,{
	
	.npc$ = "[Tournament Guide]";
	setarray .round3_name$[0],"3_1","3_2","3_3";
	setarray .round12_name$[0],"12_1","12_2";
	.round3$ = .round3_name$[rand(3)];
	.round12$ = .round12_name$[rand(2)];
	setarray .npc_name$[0],"1","2",.round3$,"4","5","6","7","8","9","10","11",.round12$,"13"; 
	setarray .round_name$[0],"1st","2nd","3rd","4th","5th","6th","7th","8th","9th","10th","11th","Semi-Final","Final";
	
	switch(select("Start " + .round_name$['round - 1] + " match and get reward:End Conversation")) {
	case 1:
		mes .npc$;
		mes "Passed " + .round_name$['round - 2] + " match!";
		mes "Congratulations!";
		next;
		mes .npc$;
		mes "Winners of each round will receive a reward of";
		mes "^007F00" + getitemname(6671) + "^000000";
		next;
		mes .npc$;
		mes "^007F00" + getitemname(6671) + "^000000 can be exchanged in Geffen with the ^007F00Geffen Coin Operator^000000 for special equipment.";
		next;
		mes .npc$;
		mes "The " + .round_name$['round - 1] + " match of Group A has begun.";
		enablenpc instance_npcname("gef_magic_round" + .npc_name$['round - 1]);
		close2;
		warp instance_mapname("1@ge_st"),104,50;
		donpcevent instance_npcname("gef_magic_round" + .npc_name$['round - 1]) + "::OnRound" + .npc_name$['round - 1] + "Start";
		donpcevent instance_npcname("gef_magic_mc") + "::OnRound" + .npc_name$['round - 1] + "Start";
		disablenpc instance_npcname("gef_magic_guide");
		break;
	case 2:
		mes .npc$;
		mes "Talk to me again when you are ready.";
		close;
	}
		
OnInstanceInit:
	disablenpc instance_npcname("gef_magic_guide");
	end;
}


1@ge_st,114,50,4	script	Arhi::gef_magic_round1	670,{
	
	end;
	
OnInstanceInit:
	disablenpc instance_npcname("gef_magic_round1");
	end;

OnRound1Start:
	attachrid('accountid);
	setmapflag instance_mapname("1@ge_st"),mf_noskill;
	pcblockmove getcharid(3),1;
	sleep2 2000;
	npctalk "The first match always make me nervous~!";
	sleep2 1000;
	npctalk "Can I win?";
	sleep2 1000;
	if(Sex) {
		npctalk "I guess it will be okay. This guy seems weak.";
	} else {
		npctalk "I guess it will be okay. This girl seems weak";
	}
	sleep2 1000;
	npctalk "OK just relax~!";
	sleep2 1000;
	npctalk "Ah... Finally, it's going to start soon!";
	sleep2 1000;
	npctalk "Just relax~!";
	sleep2 1000;
	hideonnpc instance_npcname("gef_magic_round1");
	monster instance_mapname("1@ge_st"),114,50,"Aruhi",1002,1,instance_npcname("gef_magic_round1")+"::OnMyMobDead";
	pcblockmove getcharid(3),0;
	removemapflag instance_mapname("1@ge_st"),mf_noskill;
	sleep2 180000;	if(mobcount(instance_mapname("1@ge_st"),instance_npcname("gef_magic_round1")+"::OnMyMobDead") > 0) {
		killmonster instance_mapname("1@ge_st"),instance_npcname("gef_magic_round1")+"::OnMyMobDead";
		donpcevent instance_npcname("gef_magic_mc") + "::OnRoundEnd";
	}
	disablenpc instance_npcname("gef_magic_round1");
	end;
	
OnMyMobDead:
	hideoffnpc instance_npcname("gef_magic_round1");
	donpcevent instance_npcname("gef_magic_mc") + "::OnRound1Won";
	sleep2 5000;
	npctalk "How can I talk to my family...";
	sleep2 2000;
	npctalk "So shameful...";
	sleep2 2000;
	npctalk "Ah.... My friends will laugh at me..";
	sleep2 2000;
	npctalk "Sob...";
	sleep2 2000;
	hideonnpc instance_npcname("gef_magic_round1");
	disablenpc instance_npcname("gef_magic_round1");
	end;

}

1@ge_st,114,50,4	script	Dio Anemos::gef_magic_round2	669,{
	
	end;

OnInstanceInit:
	disablenpc instance_npcname("gef_magic_round2");
	end;
	
OnRound2Start:
	attachrid('accountid);
	setmapflag instance_mapname("1@ge_st"),mf_noskill;
	pcblockmove getcharid(3),1;
	npctalk "The 2nd match has started.";
	sleep2 1000;
	npctalk "You look like weak competition.";
	sleep2 1000;
	npctalk "I think I have a good chance...";
	sleep2 1000;
	npctalk "I wish the match would start as soon as possible.";
	sleep2 1000;
	npctalk "Finish it quickly and we can get some cookies.";
	sleep2 1000;
	npctalk "Okay then. Let's start.";
	sleep2 2000;
	hideonnpc instance_npcname("gef_magic_round2");
	monster instance_mapname("1@ge_st"),114,50,"Dio Anemos",1002,1,instance_npcname("gef_magic_round2")+"::OnMyMobDead";
	pcblockmove getcharid(3),0;
	removemapflag instance_mapname("1@ge_st"),mf_noskill;
	sleep2 180000;	if(mobcount(instance_mapname("1@ge_st"),instance_npcname("gef_magic_round2")+"::OnMyMobDead") > 0) {
		killmonster instance_mapname("1@ge_st"),instance_npcname("gef_magic_round2")+"::OnMyMobDead";
		donpcevent instance_npcname("gef_magic_mc") + "::OnRoundEnd";
	}
	disablenpc instance_npcname("gef_magic_round2");
	end;
	
OnMyMobDead:
	hideoffnpc instance_npcname("gef_magic_round2");
	donpcevent instance_npcname("gef_magic_mc") + "::OnRound2Won";
	sleep2 5000;
	npctalk "Oh my god...";
	sleep2 2000;
	npctalk "I lost...";
	sleep2 2000;
	npctalk "Ah, Ah... to this random loser.";
	sleep2 2000;
	npctalk "My skill has gotten worse...";
	sleep2 2000;
	npctalk "Ah, Ah.... sad..";
	sleep2 2000;
	npctalk "Ahhhh why?";
	sleep2 2000;
	hideonnpc instance_npcname("gef_magic_round2");
	disablenpc instance_npcname("gef_magic_round2");
	end;
}