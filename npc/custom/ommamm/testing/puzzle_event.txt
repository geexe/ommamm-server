prt_monk,185,232,3	script	Event Puzzler	97,{
		//Test menu----------------------------------
		/*
		switch(select("reset:play")) {
			case 1:
				.reward_grant = 0;
				mes "Reset complete";
				close;
			case 2:
		}
		*/
		//--------------------------------------------
		if (.reward_grant) {
			mes "[Event Puzzler]";
			mes "This round is already over.";
			close;
		}
		mes "[Event Puzzler]";
		mes "Please enter a 3 digit";
		mes "number. Do not use a";
		mes "single number more than";
		mes "once or use the number 0.";
		next;
		while (1) {
			set .@yagu100, rand(1,9);
			set .@yagu10, rand(1,9);
			set .@yagu1, rand(1,9);
			if (((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1)) && (.@yagu10 != .@yagu1))
				break;
		}
		while (1) {
			while (1) {
				input .@input;
				if (.@input < 100 || .@input > 999) {
					mes "[Event Puzzler]";
					mes "Number input";
					mes "requirement has";
					mes "not been fulfilled.";
					mes "Please enter a";
					mes "3 digit number.";
					close;
				}
				set .@input100, .@input / 100;
				set .@input10, (.@input % 100) / 10;
				if (((.@input100 > 0) && (.@input10 > 0)) && (.@input % 10 > 0)) {
					if (((.@input100 != .@input10) && (.@input100 != .@input % 10)) && (.@input10 != .@input % 10))
						break;
					mes "[Event Puzzler]";
					mes "Violation of number";
					mes "input parameter. The";
					mes "number 0 has been input,";
					mes "or a number has been";
					mes "input more than once.";
					next;
				}
			}
			set .@retry, .@retry + 1;
			mes "[Event Puzzler]";
			mes "You have input...";
			mes "^0000ff" + .@input100 + "^000000, ^0000ff" + .@input10 + "^000000, ^0000ff" + (.@input % 10) + "^000000";
			mes " ";
			mes "Calculating Results...";
			mes "Please wait a moment...";
			next;
			set .@strike, 0;
			set .@ball, 0;
			if (.@yagu100 == .@input100) set .@strike, .@strike + 1;
			if (.@yagu10 == .@input10) set .@strike, .@strike + 1;
			if (.@yagu1 == .@input % 10) set .@strike, .@strike + 1;
			if ((.@yagu100 == .@input10) || (.@yagu100 == .@input % 10)) set .@ball, .@ball + 1;
			if ((.@yagu10 == .@input100) || (.@yagu10 == .@input % 10)) set .@ball, .@ball + 1;
			if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10)) set .@ball, .@ball + 1;
			if (.@strike == 3) {
				//Set variable to turn off NPC
				.reward_grant = 1;
				//Annouce the winner and round closure
				announce strcharinfo(0) + " has won the puzzle game for this round. Puzzle is now closed until next round.",bc_all;
				mes "[Event Puzzler]";
				mes "Input number accepted.";
				mes "Access authorized.";
				next;
				mes "[Event Puzzler]";
				mes "^3355FFCongratulation!!!";
				mes "You are the first person";
				mes "who got the right answer";
				mes "Please take the reward^000000";
				next;
				mes "[Event Puzzler]";
				mes "^4d4dffYou have accquired TCG 10ea^000000";
				getitem 7227,10; //TCG
				close;
				
			}
			else {
				mes "[Screen]";
				mes "*Beeeeep*";
				mes "Unauthorized";
				mes "numerical sequence.";
				next;
				mes "[Screen]";
				mes "Correct number";
				mes "in correct place";
				mes "in sequence total: ^FF0000" + .@strike + "^000000";
				mes " ";
				mes "Correct number total: ^FF0000" + .@ball + "^000000";
				next;
				if (.@retry > 4) {
					mes "[Screen]";
					mes "Correct number";
					mes "authorization";
					mes "sequence was...";
					mes "^ff0000" + .@yagu100 + "^000000, ^ff0000" + .@yagu10 + "^000000, ^ff0000" + .@yagu1 + "^000000";
					mes "Authorization number";
					mes "will change upon retry.";
					close;
				}
			}
		}
	end;
	
OnInit:
	.reward_grant = 1;
	end;

OnMinute30:
	.reward_grant = 0;
	announce "Puzzle is now open for contest. You can join at " + strnpcinfo(0) + " npc at 185,123" ,bc_all;
	end;
}